buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"
    }
}


plugins {
    id 'kotlin'
    id 'org.jetbrains.intellij' version '0.3.4'
}
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'io.gitlab.arturbosch.detekt'

repositories {
    jcenter()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

detekt {
    toolVersion = detekt_version
    input = files("src/main/kotlin")
    filters = ".*/resources/.*,.*/build/.*"
    config = files("$rootDir/detekt-config.yml")
}

tasks["detekt"].dependsOn(":custom-detekt-rules:assemble")

shadowJar {
    classifier = null
    version = road_runner_version
}

jar.enabled = false

jar.dependsOn shadowJar

dependencies {
    implementation project(':core')
    implementation project(':gui')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detekt_version"
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-cli:$detekt_version"

    detektPlugins project(':custom-detekt-rules')
}

intellij {
    version '2018.3.4'
    pluginName 'road-runner-plugin'
    plugins 'android'
    updateSinceUntilBuild false
    if (System.env.RR_IDE_PATH) {
        alternativeIdePath System.env.RR_IDE_PATH ?: ''
    }
}

group 'com.acmerobotics.roadrunner'
version road_runner_version
